buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

dependencies {
    implementation 'com.squareup:tape:1.2.3'
    implementation 'io.github.pilgr:paperdb:2.7.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

android {
    compileSdkVersion 32

    defaultConfig {
        targetSdkVersion 32
        minSdkVersion 24
    }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
    androidTest.setRoot('../test/SalesforceAnalyticsTest')
    androidTest {
      java.srcDirs = ['../test/SalesforceAnalyticsTest/src']
      resources.srcDirs = ['../test/SalesforceAnalyticsTest/src']
      res.srcDirs = ['../test/SalesforceAnalyticsTest/res']
    }
  }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/DEPENDENCIES', 'META-INF/NOTICE']
        }
    }
    defaultConfig {
    testApplicationId "com.salesforce.androidsdk.analytics.tests"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
    lint {
        abortOnError false
        xmlReport true
    }

}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.salesforce'
            artifactId 'SalesforceAnalytics'
            version project.sdkVersion
            artifact("$buildDir/outputs/aar/SalesforceAnalytics-release.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                    dependency.appendNode('scope', "compile")
                }
            }
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/18109833/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Deploy-Token"
                value ="${project.deployToken}" // the token is stored in ~/.gradle/gradle.properties
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.salesforce.mobilesdk'
    PUBLISH_VERSION = '10.0.0'
    PUBLISH_ARTIFACT_ID = 'SalesforceAnalytics'
}


